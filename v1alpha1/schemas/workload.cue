package schemas

import (
	"strings"
)

/////////////////////////////////////////////////////////////////
//// Container Schemas
/////////////////////////////////////////////////////////////////

// Container specification
#ContainerSchema: {
	// Name of the container
	name!: string

	// Container image (e.g., "nginx:latest")
	image!: #Image

	// Image pull policy
	// Deprecated: use #Image.pullPolicy instead.
	// imagePullPolicy: "Always" | "IfNotPresent" | "Never" | *"IfNotPresent"

	// Ports exposed by the container
	ports?: [portName=string]: #PortSchema & {name: portName} // Name is automatically set to the key in the ports map

	// Environment variables for the container
	env?: [envName=string]: #EnvVarSchema & {name: envName} // Name is automatically set to the key in the env map

	// Command to run in the container
	command?: [...string]

	// Arguments to pass to the command
	args?: [...string]

	// Resource requirements for the container
	resources?: #ResourceRequirementsSchema

	// Volume mounts for the container
	volumeMounts?: [string]: #VolumeMountSchema // Name is automatically set to the key in the volumeMounts map

	// Probes for health checking (primarily for sidecar containers).
	// See #ProbeSchema for K8s init container constraints.
	livenessProbe?:  #ProbeSchema
	readinessProbe?: #ProbeSchema
	startupProbe?:   #ProbeSchema
}

// Image specification for container images, used in #ContainerSchema
// Borrowed from timoni's #Image schema
#Image: {

	// Repository is the address of a container registry repository.
	// An image repository is made up of slash-separated name components, optionally
	// prefixed by a registry hostname and port in the format [HOST[:PORT_NUMBER]/]PATH.
	repository!: string

	// Tag identifies an image in the repository.
	// A tag name may contain lowercase and uppercase characters, digits, underscores, periods and dashes.
	// A tag name may not start with a period or a dash and may contain a maximum of 128 characters.
	tag!: string & strings.MaxRunes(128)

	// Digest uniquely and immutably identifies an image in the repository.
	// Spec: https://github.com/opencontainers/image-spec/blob/v1.1.1/descriptor.md#digests.
	digest!: string

	// PullPolicy defines the pull policy for the image.
	// By default, it is set to IfNotPresent.
	pullPolicy: *"IfNotPresent" | "Always" | "Never"

	// Reference is the image address computed from repository, tag and digest
	// in the format [REPOSITORY]:[TAG]@[DIGEST].
	reference: string

	if digest != "" && tag != "" {
		reference: "\(repository):\(tag)@\(digest)"
	}

	if digest != "" && tag == "" {
		reference: "\(repository)@\(digest)"
	}

	if digest == "" && tag != "" {
		reference: "\(repository):\(tag)"
	}

	if digest == "" && tag == "" {
		reference: "\(repository):latest"
	}
}

#EnvVarSchema: {
	name:   string
	value!: string
}

#ResourceRequirementsSchema: {
	requests?: {
		cpu?:    number | string & =~"^[0-9]+m$"
		memory?: number | string & =~"^[0-9]+[MG]i$"
	}
	limits?: {
		cpu?:    number | string & =~"^[0-9]+m$"
		memory?: number | string & =~"^[0-9]+[MG]i$"
	}
}

// Probe specification used by liveness, readiness, and startup probes.
// Valid for sidecar containers: all three probe types are supported.
// Valid for init containers: only startupProbe is honoured by Kubernetes
// (traditional init containers run to completion; native sidecar init containers
// with restartPolicy: Always support all three probes, requires K8s >= 1.28).
#ProbeSchema: {
	httpGet?: {
		path!: string
		port!: uint & >0 & <65536
	}
	exec?: {
		command!: [...string]
	}
	tcpSocket?: {
		port!: uint & >0 & <65536
	}
	initialDelaySeconds?: uint | *0
	periodSeconds?:       uint | *10
	timeoutSeconds?:      uint | *1
	successThreshold?:    uint | *1
	failureThreshold?:    uint | *3
}

//////////////////////////////////////////////////////////////////
//// Scaling Schema
//////////////////////////////////////////////////////////////////

#ScalingSchema: {
	count: int & >=1 & <=1000 | *1
	auto?: #AutoscalingSpec
}

#AutoscalingSpec: {
	min!: int & >=1
	max!: int & >=1
	metrics!: [_, ...#MetricSpec]
	behavior?: {
		scaleUp?: {stabilizationWindowSeconds?: int}
		scaleDown?: {stabilizationWindowSeconds?: int}
	}
}

#MetricSpec: {
	type!:   "cpu" | "memory" | "custom"
	target!: #MetricTargetSpec
	if type == "custom" {
		metricName!: string
	}
}

#MetricTargetSpec: {
	averageUtilization?: int & >=1 & <=100
	averageValue?:       string
}

//////////////////////////////////////////////////////////////////
//// Sizing Schema
//////////////////////////////////////////////////////////////////

#SizingSchema: {
	resources?:   #ResourceRequirementsSchema
	autoScaling?: #VerticalScalingSchema
}

#VerticalScalingSchema: {}

//////////////////////////////////////////////////////////////////
//// RestartPolicy Schema
//////////////////////////////////////////////////////////////////

#RestartPolicySchema: "Always" | "OnFailure" | "Never" | *"Always"

//////////////////////////////////////////////////////////////////
//// UpdateStrategy Schema
//////////////////////////////////////////////////////////////////

#UpdateStrategySchema: {
	type: "RollingUpdate" | "Recreate" | "OnDelete" | *"RollingUpdate"
	if type == "RollingUpdate" {
		rollingUpdate?: {
			maxUnavailable?: uint | string | *1
			maxSurge?:       uint | string | *1
			partition?:      uint
		}
	}
}

//////////////////////////////////////////////////////////////////
//// InitContainers Schema
//////////////////////////////////////////////////////////////////

#InitContainersSchema: #ContainerSchema

//////////////////////////////////////////////////////////////////
//// SidecarContainers Schema
//////////////////////////////////////////////////////////////////

#SidecarContainersSchema: #ContainerSchema

//////////////////////////////////////////////////////////////////
//// JobConfig Schema
//////////////////////////////////////////////////////////////////

#JobConfigSchema: {
	completions?:             uint | *1
	parallelism?:             uint | *1
	backoffLimit?:            uint | *6
	activeDeadlineSeconds?:   uint | *300
	ttlSecondsAfterFinished?: uint | *100
}

//////////////////////////////////////////////////////////////////
//// CronJobConfig Schema
//////////////////////////////////////////////////////////////////

#CronJobConfigSchema: {
	scheduleCron!:               string
	concurrencyPolicy?:          "Allow" | "Forbid" | "Replace" | *"Allow"
	startingDeadlineSeconds?:    uint
	successfulJobsHistoryLimit?: uint | *3
	failedJobsHistoryLimit?:     uint | *1
}

//////////////////////////////////////////////////////////////////
//// Stateless Workload Schema
//////////////////////////////////////////////////////////////////

#StatelessWorkloadSchema: {
	container:       #ContainerSchema
	scaling?:        #ScalingSchema
	restartPolicy?:  #RestartPolicySchema
	updateStrategy?: #UpdateStrategySchema
	sidecarContainers?: [...#SidecarContainersSchema]
	initContainers?: [...#InitContainersSchema]
	securityContext?: #SecurityContextSchema
}

//////////////////////////////////////////////////////////////////
//// Stateful Workload Schema
//////////////////////////////////////////////////////////////////

#StatefulWorkloadSchema: {
	container:       #ContainerSchema
	scaling?:        #ScalingSchema
	restartPolicy?:  #RestartPolicySchema
	updateStrategy?: #UpdateStrategySchema
	sidecarContainers?: [...#SidecarContainersSchema]
	initContainers?: [...#InitContainersSchema]
	serviceName?: string
	volumes: [string]: #VolumeSchema
	securityContext?: #SecurityContextSchema
}

//////////////////////////////////////////////////////////////////
//// Daemon Workload Schema
//////////////////////////////////////////////////////////////////

#DaemonWorkloadSchema: {
	container:       #ContainerSchema
	restartPolicy?:  #RestartPolicySchema
	updateStrategy?: #UpdateStrategySchema
	sidecarContainers?: [...#SidecarContainersSchema]
	initContainers?: [...#InitContainersSchema]
	securityContext?: #SecurityContextSchema
}

//////////////////////////////////////////////////////////////////
//// Task Workload Schema
//////////////////////////////////////////////////////////////////

#TaskWorkloadSchema: {
	container:      #ContainerSchema
	restartPolicy?: "OnFailure" | "Never" | *"Never"
	jobConfig?:     #JobConfigSchema
	sidecarContainers?: [...#SidecarContainersSchema]
	initContainers?: [...#InitContainersSchema]
	securityContext?: #SecurityContextSchema
}

//////////////////////////////////////////////////////////////////
//// Scheduled Task Workload Schema
//////////////////////////////////////////////////////////////////

#ScheduledTaskWorkloadSchema: {
	container:      #ContainerSchema
	restartPolicy?: "OnFailure" | "Never" | *"Never"
	cronJobConfig!: #CronJobConfigSchema
	sidecarContainers?: [...#SidecarContainersSchema]
	initContainers?: [...#InitContainersSchema]
	securityContext?: #SecurityContextSchema
}

//////////////////////////////////////////////////////////////////
//// DisruptionBudget Schema
//////////////////////////////////////////////////////////////////

// Availability constraints during voluntary disruptions.
// Exactly one of minAvailable or maxUnavailable must be set.
#DisruptionBudgetSchema: {
	minAvailable!: int | string & =~"^[0-9]+%$"
} | {maxUnavailable!: int | string & =~"^[0-9]+%$"
}

//////////////////////////////////////////////////////////////////
//// GracefulShutdown Schema
//////////////////////////////////////////////////////////////////

// Termination behavior for graceful workload shutdown
#GracefulShutdownSchema: {
	// Grace period before forceful termination (must be non-negative)
	terminationGracePeriodSeconds: uint | *30
	// Command to run before SIGTERM is sent
	preStopCommand?: [...string]
}

//////////////////////////////////////////////////////////////////
//// Placement Schema
//////////////////////////////////////////////////////////////////

// Provider-agnostic workload placement intent
#PlacementSchema: {
	// Failure domain distribution target
	spreadAcross?: *"zones" | "regions" | "hosts"
	// Node/host selection criteria (string-to-string map)
	requirements?: [string]: string
	// Escape hatch for provider-specific placement details
	platformOverrides?: {...}
}
